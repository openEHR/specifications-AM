= Templates

== Overview

In ADL2, a template is a kind of archetype that uses one feature of ADL in particular to express composition of archetypes: the ability to state 'slot fillers'. This is achieved by specialisation of an archetype, in which a specialised version of any slot can be stated, containing one or more fillers - i.e. archetypes or other templates that match the slot specification.

It is worth emphasising this point, since 'slot-filling' is usually thought of as a compositional relationship (joining archetypes), rather than a specialisation relationship. However, ADL considers a filled or partly filled slot as a kind of slot, i.e. a specialisation of an initially empty slot. The consequence of this is that no special relationship or operation is required to achieve slot-filling, which is the one ADL semantic normally only used in templates.

All of the other constraints used in templates typically occur in normal archetypes, with one exception: multiplicity reduction to `{0}` of existences, cardinalities and occurrences. This particular constraint can be used in arhetypes, but is generally only useful in templates, since one of the main functions of a template is to select specific data items from archetypes composed in a structure.

ADL provides one other special feature specific to templates: the ability to define 'overlays'. An overlay is just a specialised archetype that is local to the template, rather than being a self-standing archetype. It is used to enable a template to fill a slot not only with an available normal archetype, but with a locally constrained version of an archetype. This feature enables a template to express constraints on a filler archetype without need to create new independent archetype.

== Example

By way of illustrating ADL's template-related ADL features, an example is useful. The following is an openEHR template of the class `COMPOSITION`, representing a patient's clinical information discharge summary. The 

[source, adl]
--------
template (adl_version=2.0.5; rm_release=1.0.2)
	openEHR-EHR-COMPOSITION.t_clinical_info_ds_sf.v1.0.0

specialize
	openEHR-EHR-COMPOSITION.discharge.v1

language
	original_language = <[ISO_639-1::en]>

description
	lifecycle_state = <"unmanaged">
	original_author = <
		["name"] = <"Ian McNicoll">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"ian.mcnicoll@oceaninformatics.com">
		["date"] = <"01/04/2011">
	>
	copyright = <"copyright (c) 2011 openEHR Foundation">
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Templated clinical COMPOSITION for Simple discharge summary">
		>
	>

definition
	COMPOSITION[id1.1] matches {	-- Clinical detail
		context existence matches {0} 
		content matches {
			use_archetype SECTION[id0.1, openEHR-EHR-SECTION.t_patient_event_info_ds_sf.v1] occurrences matches {1} 
			use_archetype EVALUATION[id0.2, openEHR-EHR-EVALUATION.t_clinical_info_ds_sf-1.v1] occurrences matches {0..1} 
			use_archetype EVALUATION[id0.3, openEHR-EHR-EVALUATION.t_clinical_info_ds_sf-2.v1] 
			use_archetype EVALUATION[id0.4, openEHR-EHR-EVALUATION.t_clinical_info_ds_sf-3.v1] 
			use_archetype EVALUATION[id0.5, openEHR-EHR-EVALUATION.t_clinical_info_ds_sf-4.v1] occurrences matches {1..*} 
			use_archetype EVALUATION[id0.6, openEHR-EHR-EVALUATION.t_clinical_info_ds_sf-5.v1] occurrences matches {0..1} 
			use_archetype EVALUATION[id0.7, openEHR-EHR-EVALUATION.t_clinical_info_ds_sf-6.v1] occurrences matches {0..1} 
			use_archetype EVALUATION[id0.8, openEHR-EHR-EVALUATION.t_clinical_info_ds_sf-7.v1] occurrences matches {0..*} 
			use_archetype EVALUATION[id0.9, openEHR-EHR-EVALUATION.t_clinical_info_ds_sf-8.v1] 
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Clinical detail">
				description = <"Clinical detail of Simple discharge summary">
			>
			["id0.1"] = <
				text = <"Event data">
				description = <"Event data">
			>
			["id0.2"] = <
				text = <"Allergies & adverse reactions">
				description = <"Allergies & adverse reactions">
			>
			["id0.3"] = <
				text = <"Alerts">
				description = <"Alerts">
			>
			["id0.4"] = <
				text = <"Clinical synopsis">
				description = <"Clinical synopsis">
			>
			["id0.5"] = <
				text = <"Diagnosis">
				description = <"Diagnosis">
			>
			["id0.6"] = <
				text = <"Investigations">
				description = <"Investigations">
			>
			["id0.7"] = <
				text = <"Medications">
				description = <"Medications">
			>
			["id0.8"] = <
				text = <"Procedures">
				description = <"Procedures">
			>
			["id0.9"] = <
				text = <"Medical certificate">
				description = <"Medical certificate">
			>
		>
	>
-------------------------------------------------------------
template_overlay
	openEHR-EHR-EVALUATION.t_clinical_info_ds_sf-1.v1.0.0

specialize
	openEHR-EHR-EVALUATION.adverse.v1

definition
	EVALUATION[id1.1] matches {	-- Adverse reaction details for episode of care
		/data[id3]/items matches {
			ELEMENT[id11] occurrences matches {0} 
		}
		/data[id3]/items[id20]/items matches {
			ELEMENT[id33] occurrences matches {0} 
			ELEMENT[id16] occurrences matches {0} 
			ELEMENT[id5] occurrences matches {0} 
			ELEMENT[id21] occurrences matches {0} 
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Adverse reaction details for episode of care">
				description = <"Adverse reaction details for episode of care">
			>
		>
	>
-------------------------------------------------------------
template_overlay
	openEHR-EHR-EVALUATION.t_clinical_info_ds_sf-2.v1.0.0

specialize
	openEHR-EHR-EVALUATION.alert.v1

definition
	EVALUATION[id1.1] matches {	-- Alerts
		/data[id2]/items matches {
			ELEMENT[id3] occurrences matches {0} 
			ELEMENT[id6] occurrences matches {0} 
			ELEMENT[id10] occurrences matches {0} 
			ELEMENT[id15] occurrences matches {0} 
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Alerts">
				description = <"Alert details">
			>
		>
	>
-------------------------------------------------------------
template_overlay
	openEHR-EHR-EVALUATION.t_clinical_info_ds_sf-3.v1.0.0

specialize
	openEHR-EHR-EVALUATION.clinical_synopsis.v1

definition
	EVALUATION[id1.1] matches {	-- Clinical synopsis
		/data[id2]/items matches {
			ELEMENT[id3.1] occurrences matches {1} matches {	-- Description
				name matches {
					DV_TEXT[id0.1] 
					DV_CODED_TEXT[id0.2] matches {
						defining_code matches {[ac0.1]}		-- Description (synthesised)
					}
				}
			}
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Clinical synopsis">
				description = <"Clinical synopsis">
			>
			["id3.1"] = <
				text = <"Description">
				description = <"Clinical synopsis description">
			>
			["at0.1"] = <
				text = <"Admission">
				description = <"Admission">
			>
			["at0.2"] = <
				text = <"Clinical note">
				description = <"Clinical note">
			>
			["at0.3"] = <
				text = <"Post discharge care">
				description = <"Post discharge care">
			>
			["at0.4"] = <
				text = <"Inpatient episode">
				description = <"Inpatient episode">
			>
			["at0.5"] = <
				text = <"Main complaints">
				description = <"Main complaints">
			>
			["at0.6"] = <
				text = <"Past medical summary">
				description = <"Past medical summary">
			>
			["at0.7"] = <
				text = <"Clinical progress">
				description = <"Clinical progress">
			>
			["at0.8"] = <
				text = <"Suggested treatments">
				description = <"Suggested treatments">
			>
			["at0.9"] = <
				text = <"Social history">
				description = <"Social history">
			>
			["at0.10"] = <
				text = <"Past medical history">
				description = <"Past medical history">
			>
			["at0.11"] = <
				text = <"Presenting complaints/physical findings">
				description = <"Presenting complaints/physical findings">
			>
			["at0.12"] = <
				text = <"Progress summary">
				description = <"Progress summary">
			>
			["at0.13"] = <
				text = <"Significant physical findings">
				description = <"Significant physical findings">
			>
			["ac0.1"] = <
				text = <"Description (synthesised)">
				description = <"Clinical synopsis description (synthesised)">
			>
		>
	>
	value_sets = <
		["ac0.1"] = <
			id = <"ac0.1">
			members = <"at0.1", "at0.2", "at0.3", "at0.4", "at0.5", "at0.6", "at0.7", "at0.8", "at0.9", "at0.10", "at0.11", "at0.12", "at0.13">
		>
	>
-------------------------------------------------------------
template_overlay
	openEHR-EHR-EVALUATION.t_clinical_info_ds_sf-4.v1.0.0

specialize
	openEHR-EHR-EVALUATION.problem-diagnosis-simple.v1

definition
	EVALUATION[id1.1.1.1] matches {	-- Simple problem / diagnosis summary
		/data[id2]/items matches {
			ELEMENT[id3.1.0.1] occurrences matches {1} 	-- Diagnosis
			ELEMENT[id0.32] occurrences matches {0} 
			ELEMENT[id4] occurrences matches {0} 
			ELEMENT[id5] occurrences matches {0} 
			ELEMENT[id6] occurrences matches {0} 
			ELEMENT[id10.0.0.1] 	-- Additional remarks
			ELEMENT[id39] occurrences matches {0} 
			ELEMENT[id11] occurrences matches {0} 
			ELEMENT[id38] occurrences matches {0} 
			CLUSTER[id12] occurrences matches {0} 
			ELEMENT[id32] occurrences matches {0} 
			CLUSTER[id0.35] occurrences matches {0} 
			ELEMENT[id0.0.55.1] occurrences matches {1} 	-- Diagnosis type
		}
		/protocol existence matches {0} 
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1.1.1"] = <
				text = <"Simple problem / diagnosis summary">
				description = <"Simple problem / diagnosis summary">
			>
			["id3.1.0.1"] = <
				text = <"Diagnosis">
				description = <"Diagnosis">
			>
			["id10.0.0.1"] = <
				text = <"Additional remarks">
				description = <"Diagnosis/problem additional remarks">
			>
			["id0.0.55.1"] = <
				text = <"Diagnosis type">
				description = <"Diagnosis type">
			>
		>
	>
-------------------------------------------------------------
template_overlay
	openEHR-EHR-EVALUATION.t_clinical_info_ds_sf-5.v1.0.0

specialize
	openEHR-EHR-EVALUATION.clinical_synopsis.v1

definition
	EVALUATION[id1.1] matches {	-- Investigations summary
		/data[id2]/items matches {
			ELEMENT[id3.1] 	-- Description
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Investigations summary">
				description = <"Summary of investigations undertaken during stay">
			>
			["id3.1"] = <
				text = <"Description">
				description = <"Summary of investigations description">
			>
		>
	>
-------------------------------------------------------------
template_overlay
	openEHR-EHR-EVALUATION.t_clinical_info_ds_sf-6.v1.0.0

specialize
	openEHR-EHR-EVALUATION.clinical_synopsis.v1

definition
	EVALUATION[id1.1] matches {	-- Medication orders summary
		/data[id2]/items matches {
			ELEMENT[id3.1] 	-- Description
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Medication orders summary">
				description = <"Summary of medication orders">
			>
			["id3.1"] = <
				text = <"Description">
				description = <"Summary of medications description">
			>
		>
	>
-------------------------------------------------------------
template_overlay
	openEHR-EHR-EVALUATION.t_clinical_info_ds_sf-7.v1.0.0

specialize
	openEHR-EHR-EVALUATION.clinical_synopsis.v1

definition
	EVALUATION[id1.1] matches {	-- Procedures summary
		/data[id2]/items matches {
			ELEMENT[id3.1] 	-- Description
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Procedures summary">
				description = <"Summary of procedures">
			>
			["id3.1"] = <
				text = <"Description">
				description = <"Summary of procedures undertaken during episode of care">
			>
		>
	>
-------------------------------------------------------------
template_overlay
	openEHR-EHR-EVALUATION.t_clinical_info_ds_sf-8.v1.0.0

specialize
	openEHR-EHR-EVALUATION.medical_certificate.v1


definition
	EVALUATION[id1.1] matches {	-- Simple Discharge Summary Medical Certificate
		/data[id2]/items matches {
			ELEMENT[id3] occurrences matches {0} 
			ELEMENT[id4] occurrences matches {0} 
			ELEMENT[id5] occurrences matches {0} 
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Simple Discharge Summary Medical Certificate">
				description = <"Only used for Discharge Summary Document">
			>
		>
	>
--------
